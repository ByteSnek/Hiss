plugins {
    id 'java'
    id 'maven-publish'
}

group = mavenGroup
version = projectVersion

sourceSets {
    create('gradle')
}

configurations {
    gradleCompileOnly.extendsFrom compileOnly
    gradleImplementation.extendsFrom implementation
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    compileOnly 'org.jetbrains:annotations:22.0.0'

    gradleCompileOnly gradleApi()
    gradleImplementation sourceSets.main.getOutput()
}

jar {
    from sourceSets.gradle.getOutput()

    manifest {
        attributes 'FMLModType': 'GAMELIBRARY'
        attributes 'Automatic-Module-Name': mavenGroup
    }

    from file('LICENSE')
}

tasks.register('preparePublish', JavaExec) {
    group "publishing"
    mainClass = "xyz.snaker.hiss.maven.MavenRepositoryHook"
    classpath = sourceSets.main.runtimeClasspath
}

tasks.register('docJar', Jar) {
    build.dependsOn it
    from javadoc
    archiveClassifier = 'javadoc'
}

tasks.register('srcJar', Jar) {
    build.dependsOn it
    from sourceSets.main.allSource
    archiveClassifier = 'sources'
}

publish.dependsOn preparePublish

publishing {
    repositories {
        maven {
            url "http://127.0.0.1:8080/releases"
            credentials {
                username System.getenv("MVN_USER")
                password System.getenv("MVN_PASS")
            }
        }
    }
    publications {
        register('pub', MavenPublication) {
            artifact jar
            artifact srcJar
            artifact docJar
        }
    }
}